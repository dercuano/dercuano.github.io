<!DOCTYPE html>
<html><title>Quasiquote patterns ⁑ Dercuano</title><meta charset="utf-8"></meta><link href="../liabilities/style.css" rel="stylesheet"></link><meta content="width=device-width, initial-scale=1.0" name="viewport"></meta><h1>Quasiquote patterns</h1><div class="metadata">Kragen Javier Sitaker, 2007 to 2009
(9 minutes)</div><p>[Note added 2019: Darius Bacon is working on a pattern-matching
dialect of Scheme called Squeam, and I may have gotten these ideas
from talking with him about it.  It’s been long enough that I can’t
remember for sure.]</p>
<p>It occurred to me that if you wanted to write a tree-rewriting dialect
of Lisp, you could use quasiquotation both for the pattern to match
and the thing to rewrite it to.  For example, in David Andrew Kranz's
1988 (1990?) dissertation, "Orbit: An Optimizing Compiler for Scheme,"
he describes much of the CPS-conversion process in tree-rewriting
rules such as the following:</p>
<pre><code>(convert &lt;atom&gt; &lt;cont&gt;) =&gt; `(,&lt;cont&gt; ,&lt;atom&gt;)
(convert '(&lt;proc&gt; . &lt;arguments&gt;) &lt;cont&gt;) =&gt;
(convert &lt;proc&gt;
    (lambda (p)
        ,(convert-arguments &lt;arguments&gt;
                        `(,p ,&lt;cont&gt;))))
(convert-arguments '() &lt;final-call&gt;) =&gt; &lt;final-call&gt;
(convert-arguments '(argument . rest) &lt;final-call&gt;) =&gt;
    (convert &lt;argument&gt;
    `(lambda (k)
     ,(convert-arguments &lt;rest&gt;
                 (append &lt;final-call&gt; '(k)))))
</code></pre>
<p>It seems to me that these could be written in a pattern-matching
Scheme dialect as follows, without breaking backwards-compatibility:</p>
<pre><code>(def (convert atom cont) `(,cont ,atom))
(def (convert (proc . arguments) cont)
    (convert proc (lambda (p) ,(convert-arguments arguments
                          `(,p ,cont)))))
(def (convert-arguments () final-call) final-call)
(def (convert-arguments (argument . rest) final-call)
    (convert argument 
    `(lambda (k) ,(convert-arguments rest (append final-call '(k))))))
</code></pre>
<p>(I'm using "def" as a synonym for "define" here.  Or maybe a
pattern-matching extension thereof.)</p>
<p>I suspect that the original syntax was not formally specified.</p>
<p>Here are some more things specified in this style.</p>
<pre><code>(def (length ()) 0)
(def (length (a . b) (+ 1 (length b))))
(def (append () a) a)
(def (append (a . b) c) `(,a . ,(append b c)))
(def (assv a ()) '())
(def (assv a ((a . b) . r)) `(,a . ,b))
(def (assv a ((x . y) . z)) (assv a z))
(def (delv a ()) '())
(def (delv a (a . b)) (delv a b))
(def (delv a (b . c)) `(,b . ,(delv a c)))
(def (eval ('var var) env) (cadr (assv var env)))
(def (eval ('lambda args body) env) `(,args ,body ,env))
(def (eval ('apply (func-args body func-env) args) env)
    (eval body (augment-env func-args (eval-args args env) func-env)))
(def (eval ('if cond cons alt) env)
    (if (is-true (eval cond env)) (eval cons env) (eval alt env)))
</code></pre>
<p>Note the implicit pattern-matching equality tests in assv and delv.</p>
<p>This syntax has the disadvantage that the symbol "quote" is treated
specially, in order to support tagged things like the last bit.  I had
considered using full quasiquote syntax, which avoids that problem,
but it has two disadvantages: it's backwards-incompatible with
Scheme's current syntax, and worse, it's more verbose.</p>
<p>(It also lacks a way of requiring, in the pattern, that a particular
item be just a plain symbol, rather than a list or an array or
something.  You could specify, say, "unquote" or "symbol" to mean this,
and write (symbol x) or ,x to mean "some x, which is required to be a
symbol".)</p>
<p>If you add special treatment for the symbol "..." to the declaration
syntax, as in the Scheme macro system, then you get an arguably
superior syntax for list tails with a special secret feature.</p>
<pre><code>(def (append () x) x)
(def (append (x xs ...) y) `(x . ,(append xs y)))
(def (assv a ()) '())
(def (assv x ((x xs ...) zs ...)) `(x . xs))
(def (assv x ((y ys ...) zs ...)) (assv x zs))
(def (delv x ()) '())
(def (delv x (x xs ...)) (delv x xs))
(def (delv x (y ys ...)) `(,x . ,(delv x ys)))
</code></pre>
<p>The special secret feature is that it generalizes to some kinds of
restructuring:</p>
<pre><code>(def (augment-env () () env) env)
(def (augment-env (var vars ...) (val vals ...) env)
 (augment-env vars vals `((,var . ,val) . ,env)))
</code></pre>
<p>(The above definitions of augment-env, assv, and eval constitute a
complete interpreter for a Turing-complete language.)</p>
<p>Pattern-matching simplifies writing compilers in general.  Here's a
compiler into the language above from a subset of Scheme:</p>
<pre><code>(def (compile ('lambda vars body)) `(lambda ,vars ,(compile body))))
(def (compile ('if cond conseq alt)) `(if ,cond ,conseq ,alt)))
(def (compile (f args ...)) 
 `(apply ,(compile f) ,(compile-args args)))
(def (compile-args ()) '())
(def (compile-args (arg args ...)) 
 `(,(compile arg) . ,(compile-args args)))
(def (compile x) `(var ,x))  ; or (def (compile (symbol x)) `(var ,x))
</code></pre>
<p>Here's a little recursive-descent compiler for the following grammar.
(Our input strings here are Lisp lists.)</p>
<pre><code>mul-expr  ::= atom-expr * mul-expr | atom-expr / mul-expr | atom-expr
atom-expr ::= &lt;number&gt; | &lt;symbol&gt; | (expr)
expr      ::= mul-expr + expr | mul-expr - expr | mul-expr
</code></pre>
<p>Each parse routine takes a suffix of the whole input string, and
returns a list of two lists: the compiled version of what it parsed,
and the remaining suffix.</p>
<pre><code>(def (compose-parser inner outer tokens)
 ((lambda ((inner-result leftovers))
    `(,(outer inner-result) ,leftovers))
  (inner tokens)))
(def (parse-atom ((expr) stuff ...)) `(,(parse expr) ,stuff))
(def (parse-atom (x stuff ...))
 `(,(if (symbol? x) `(var ,x) `(const ,x)) ,stuff))
(def (parse-expr stuff) (parse-expr-tail (parse-mul-expr stuff)))
(def (parse-expr-tail (compiled ('+ rest ...)))
 (op-node '+ compiled rest parse-expr))
(def (parse-expr-tail (compiled ('- rest ...)))
 (op-node '- compiled rest parse-expr))
(def (op-node op compiled rest inner)
 (compose-parser inner
         (lambda (parsed-rest) `(,op ,compiled ,parsed-rest))
         rest))
(def (parse-expr-tail (compiled other)) `(,compiled ,other))
(def (parse-mul-expr stuff) (parse-mul-expr-tail (parse-atom stuff)))
(def (parse-mul-expr-tail (compiled ('* rest ...)))
 (op-node '* compiled rest parse-mul-expr))
(def (parse-mul-expr-tail (compiled ('/ rest ...)))
 (op-node '/ compiled rest parse-mul-expr))
(def (parse-mul-expr-tail (compiled other) `(,compiled ,other)))
</code></pre>
<p>That's pretty ugly!  And it doesn't even quite compile into RPN; you
still need this:</p>
<pre><code>(def (rpnify ('+ a b)) (rpnify-op '+ a b))
(def (rpnify ('- a b)) (rpnify-op '- a b))
(def (rpnify ('* a b)) (rpnify-op '* a b))
(def (rpnify ('/ a b)) (rpnify-op '/ a b))
(def (rpnify-op op a b) (append (rpnify a) (append (rpnify b) `(,op))))
(def (rpnify ('var x)) `(var ,x))
(def (rpnify ('const x)) `(const ,x))
</code></pre>
<p>That's kind of ugly in its runtime; a cleverer implementation would
walk the op tree from right to left, passing along an accumulator of
ops as it went:</p>
<pre><code>(def (walk ('+ a b) ac) (walk-op '+ a b ac))
(def (walk ('- a b) ac) (walk-op '- a b ac))
(def (walk ('* a b) ac) (walk-op '* a b ac))
(def (walk ('/ a b) ac) (walk-op '/ a b ac))
(def (walk-op op a b ac) (walk a (walk b `(,op . ,ac))))
(def (walk ('var x) ac) `((var ,x) . ,ac))
(def (walk ('const x) ac) `((const ,x) . ,ac))
</code></pre>
<p>I should probably write a version that does that in a single pass.
But first!  Something to run the pattern-matching.<br />
(match pattern actual bindings).</p>
<pre><code>(def (match () () vars) vars)
(def (match () other vars) #f)
(def (match ('quote x) x vars) vars)
(def (match ('quote x) other vars) #f)
(def (match ('symbol x) (symbol y) vars) (match x y vars))
(def (match ('symbol x) other vars) #f)
(def (match (ap . bp) (a . b) vars)
 (let ((new-vars (match ap a vars)))
      (if new-vars (match bp b new-vars))))
(def (match (ap . bp) other vars) #f)
(def (match (symbol x) y vars) `((,x . ,y) . ,vars))
</code></pre>
<p>That leaves out the equality-testing logic --- in both the ('symbol x)
and the (symbol x) cases, we need to verify that there's no existing
conflicting binding for x.</p>
<p>Recasting that (mostly) in a form that doesn't rely on
pattern-matching:</p>
<pre><code>(define (match pattern actual vars)
  (cond ((null? pattern) (match-null actual vars))
    ((symbol? pattern) (match-symbol pattern actual vars))
    ((pair? pattern) (match-pair pattern actual vars))
    (else (error "invalid pattern"))))
(define (match-null actual vars) (if (null? actual) vars #f))
(define (match-symbol pattern actual vars)
  `((,pattern . ,actual) . ,vars)) ; XXX doesn't check for dups
(define (match-pair pattern actual vars)
  (cond ((eq? (car pattern) 'quote) 
     (and (equal? (cadr pattern) actual) vars))
    ((eq? (car pattern) 'symbol)
     (and (symbol? (cadr pattern)) 
      (match (cadr pattern) actual vars)))
    (else 
     (let ((new-vars (match (car pattern) (car actual) vars)))
       (if new-vars (match (cdr pattern) (cdr actual) new-vars) #f)))))
</code></pre>
<p>And here's a fix to match-symbol that does equality testing:</p>
<pre><code>(define (match-symbol pattern actual vars)
    (let ((existing (assq pattern vars)))
  (cond ((not existing) `((,pattern . ,actual) . ,vars))
    ((equal? (cdr existing) actual) vars)
    (else #f))))
</code></pre>
<p>From there, it's a simple extension to supporting multiple patterns:</p>
<pre><code>(define (match-many patterns functions actual)
  (if (null? patterns) (error "match failure")
(let ((bindings (match (car patterns) actual '())))
  (if bindings ((car functions) bindings)
    (match-many (cdr patterns) (cdr functions) actual)))))
</code></pre>
<p>Ideally we'd really like to compile the pattern-matching down to
efficient Scheme code which does a reasonably small number of tests
and doesn't re-examine the pattern as well as the actual, each time
around.  I don't think I'm quite up for that yet.</p>
<p>Unsurprisingly, this is not a new idea; Andrew K. Wright's 1996
proposal for pattern-matching in Scheme uses a slight superset of this
pattern syntax, with the same semantics.  (I switched to using equal?
instead of eqv? in the above code after reading his paper.)  However,
I think it's interesting to allow definition of new cases on existing
functions this way, rather than shoving it off into a "match-define"
ghetto as Wright does.</p><script src="../liabilities/addtoc.js"></script><div><h2>Topics</h2><ul><li><a href="../topics/lisp.html">Lisp</a> (9 notes)
</li><li>Pattern matching</li></ul></div></html>